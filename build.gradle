plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.18' apply false
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    group = "de.textmode.ipdsbox"
    version = "0.0.1"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    checkstyle {
        toolVersion = "10.17.0"
        config = resources.text.fromFile("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    }

    tasks.withType(Checkstyle).configureEach {
        ignoreFailures = true
        reports {
            xml.required.set(false)
            html.required.set(true)
        }
    }

    dependencies {
        compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.0'

        testCompileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.0'
		
        testImplementation 'org.junit.jupiter:junit-jupiter:5.14.0'
        testImplementation 'org.junit.vintage:junit-vintage-engine:5.14.0'
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }
   
    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    spotbugs {
        toolVersion = "4.9.0"
        reportLevel = "low"
        excludeFilter = file("${rootProject.projectDir}/config/spotbugs/filter.xml")
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
        ignoreFailures = true
        reports {
            xml.required.set(false)
            html.required.set(true)
        }
    }

    tasks.named("check") {
        dependsOn("checkstyleMain", "spotbugsMain")
    }
}
